;
; Orpheus - Versao para cartucho
; por Daniel Jose Viana - 01-Julho-2019
;

;--------- BLOCO 1:CONSTANTES 

INCLUDE "msxbios.inc"
INCLUDE "msx_system_vars.inc"

;RDVRM: EQU 04AH 
;WRTVRM: EQU 04DH 
;LDIRVM: EQU 05CH 
;WRTPSG: EQU 093H 
;GTSTCK: EQU 0D5H 
;GTPAD:  EQU 0DEH
;GTTRIG: EQU 0D8H 
;RDVDP: EQU 13EH 
;INVRAM:EQU 200H 
TCAN: EQU 003H 
LIINV: EQU 008H 
PAUSA: EQU 080H 
COMPV: EQU 008H 
TPINV: EQU 002H 
IPVR: EQU TRAJ ; 0A000H ; TRAJETORIA



ORG 0x4000

;--------- BLOCO 0:CABECALHO E INICIALIZACAO 
CBABECALHO:
DEFB "AB" ; expansion ROM header
DEFW INITCODE ; start of the init code, 0 if no initcode
DEFW 0; pointer to CALL statement handler, 0 if no such handler
DEFW 0; pointer to expansion device handler, 0 if no such handler
DEFW 0 ; pointer to the start of a tokenized basicprogram, 0 if no basicprogram
DEFS 6,0 ; room reserved for future extensions


INITCODE:

; LIMPA A AREA DE VARIAVEIS
XOR A
LD HL,VARS
LD DE,VARS+1
LD BC,FIMVARS-VARS
LD (DE),A
LDIR

; INICIALIZA VARIAVEIS
LD A,1
LD (LIBOMB),A
LD (LISAB),A
LD HL,16384
LD (INISEM),HL
LD HL,32767
LD (FIMSEM),HL

; INICIALIZA TELA

; SCREEN 1,
LD A,32        ; width 32
LD (LINL32),A

LD A,15        ; color 15,1,1
LD (FORCLR),A
LD A,1
LD (BAKCLR),A
LD (BDRCLR),A
; LD A,1
CALL CHGMOD    ;  A=1 Screen1

CALL ERAFNK    ; keyoff INIFNK

; Sprites 16x16, keyclick off 
LD A,(RG1SAV)  ; mirror do registro 1 do VDP
SET 1,A        ; liga bit 1 - sprites 16x16
LD B,A
LD C,1
CALL WRTVDP    ; ativa sprites 16x16


; Carrega sprites 18
LD HL,SPRITES
LD DE,14336 ;(ATRBAS)
LD BC,18*32
CALL LDIRVM

; vpokes
LD HL,0   ;0 to 128 - 0x00
LD BC,128
LD A,0
CALL FILVRM

; vpoke 0,128
; vpoke 64,128
LD HL,0
LD A,128
CALL WRTVRM
LD HL,64
CALL WRTVRM

LD HL,8192  ;8192,113 caracteres 0x00 a 0x07
LD A,0x71   ; verde com fundo preto
CALL WRTVRM

INC HL      ;8193, 129 caracteres 0x08 a 0x0f
LD A,0x81   ; vermelho com fundo preto
CALL WRTVRM

LD HL,8216  ;8216,33 caracteres 
LD A,0x21   ; verde escuro com fundo preto
CALL WRTVRM

LD HL,8198  ; cor dos numeros 0-7
LD A,0xa1
CALL WRTVRM
INC HL      ; cor dos numeros 8-9
;LD A,0xa1
CALL WRTVRM

LD A,0x31
LD B,4
VPK:
INC HL      ; cor das letras
CALL WRTVRM
DJNZ VPK





; LOOP DO JOGO 220
LOOP_JOGO:
CALL TELA  ; CALL TELA

LD A,11; locate 10,11 
LD (CSRX),A
LD A,12
LD (CSRY),A

LD B,12   ; print TECLE ESPACO
LD HL,MSSPC ; MSG_TECLE_ESPACO
LL1: LD A,(HL)
INC HL
CALL CHPUT
DJNZ LL1

; 230 aguarda espaco
AGUARDA:  
CALL CHSNS
JR Z,AGUARDA
CALL CHGET
CP 32
JR NZ,AGUARDA

; 230 -> Else
CALL TELA
CALL INICIO
CALL KILBUF

JR LOOP_JOGO


; CALL INICIO
; CALL KILBUF
; GOTO LOOP_JOGO


;-------- BLOCO 2:INICIA JOGO 
INICIO: 
LD A,209 
LD HL,6996 
CALL WRTVRM 
LD HL,7000 
CALL WRTVRM 
LD HL,7004 
CALL WRTVRM 
LD HL,MSTEL 
LD DE,6144 
LD BC,32 
CALL LDIRVM 
LD A,3 
LD (NAVES),A 
LD HL,0 
LD (PONTOS),HL 
CALL PLACAR 
XOR A 
LD (LISAB),A 
LD (FASE),A 
CALL STFASE 
;-------- BLOCO 3:INICIA NAVE 
INNAVE: 
LD HL,ARPC 
LD DE,6912 
LD BC,20 
CALL LDIRVM 
LD A,160 
LD (YNAVE),A 
LD A,128 
LD (XNAVE),A 
LD A,209 
LD (YTIRO),A 
LD (YTIRO2),A 
LD A,TCAN 
LD (CCAN),A 
LD HL,512 
LD (FTIRO),HL 
CALL INIBMB 
;-------- BLOCO 4:INICIA FASE 
INFASE: 
CALL INIINV 
XOR A 
LD (CONTDS),A 
LD (PSINC),A 
LD (CCINV),A 
LD (STATX),A 
LD A,30 
OUT (153),A 
LD A,24 
OUT (153),A 
LD A,(NAVES) 
ADD A,47 
OUT (152),A 
;-------- BLOCO 5:LOOP CENTRAL 
CENTRAL: 
LD A,(CCAN) 
DEC A 
LD (CCAN),A 
AND A 
JR NZ,SEGUE 
LD A,TCAN 
LD (CCAN),A 
LD A,(MODE) 
CALL GTSTCK 
CP 3 
JR Z,DIR 
CP 7 
JR Z,ESQ 
;CP 1 
CP 5 
RET Z 
JR SEGUE 
DIR: 
LD A,(XNAVE) 
CP 234 
JR NC,SEGUE 
INC A 
LD (XNAVE),A 
JR SEGUE 
ESQ: 
LD A,(XNAVE) 
CP 16 
JR C,SEGUE 
DEC A 
LD (XNAVE),A 
JR SEGUE 
SEGUE: 
LD A,(MODE) 
CALL TRIGGER 
CP 255 
JR NZ,SEGUE2 
LD A,(YTIRO) 
CP 209 
JR NZ,TIRO2 
LD HL,6918 
LD A,16 
CALL WRTVRM 

; COLORE TIRO
;LD HL,6923 
;LD A,10 
;CALL WRTVRM 


LD A,(YNAVE) 
SUB 8 
LD (YTIRO),A 
LD A,(XNAVE) 
LD (XTIRO),A 
JR STIRO 
TIRO2: 
LD A,(YTIRO2) 
CP 209 
JR NZ,SEGUE2 
LD HL,6922 
LD A,16 
CALL WRTVRM 

; COLORE TIRO
;LD HL,6923 
;LD A,10 
;CALL WRTVRM 

;
;



LD A,(YNAVE) 
SUB 8 
LD (YTIRO2),A 
LD A,(XNAVE) 
LD (XTIRO2),A 
STIRO: 
LD HL,SDTIRO 
CALL STSND 
LD HL,100 
LD (FTIRO),HL 
SEGUE2: 
CALL IMPNAV 
CALL ESTRTM 
CALL SABOT 
LD A,(STATX) 
AND A 
JP NZ,EXPLS 
LD A,(YTIRO) 
CP 209 
JR Z,TIROX2 
LD (YTR),A 
LD A,(XTIRO) 
LD (XTR),A 
CALL TIROS 
LD A,(YTR) 
LD (YTIRO),A 
TIROX2: 
LD A,(YTIRO2) 
CP 209 
JR Z,SEGUE3 
LD (YTR),A 
LD A,(XTIRO2) 
LD (XTR),A 
CALL TIROS2 
LD A,(YTR) 
LD (YTIRO2),A 
SEGUE3: 
AND A 
AND A 
LD A,(CCINV) 
INC A 
LD (CCINV),A 
CP TPINV 
JR NZ,SEGUE4 
XOR A 
LD (CCINV),A 
CALL INVSR 
CALL ATBOMB 
SEGUE4: 
CALL TESTCH 
LD HL,(FTIRO) 
LD A,2 
CP H 
JR Z,SEGUE5 
INC HL 
INC HL 
INC HL 
INC HL 
LD (FTIRO),HL 
LD E,L 
LD A,0 
PUSH HL 
CALL WRTPSG 
POP HL 
LD E,H 
LD A,1 
CALL WRTPSG 
JR TSFASE 
SEGUE5: 
LD E,0 
LD A,8 
CALL WRTPSG 
TSFASE: 
LD A,(CONTDS) 
CP LIINV 
JR Z,TRFASE 
JP CENTRAL 
TRFASE: 
CALL STFASE 
JP INFASE 
;-------- BLOCO 6:ALEATORIZADOR 
RANDOM: 
PUSH HL 
LD HL,(SEMT) 
LD DE,(FIMSEM) 
AND A 
SBC HL,DE 
JR NZ,RAND2 
LD HL,(INISEM) 
LD (SEMT),HL 
RAND2: 
LD HL,(SEMT) 
INC HL 
LD (SEMT),HL 
LD A,(HL) 
POP HL 
RET 
;-------- BLOCO 7:DISPARA TIRO 
TRIGGER: 
CALL GTTRIG 
AND A 
JR Z,NAOPRE 
LD A,(DBOUNC) 
AND A 
JR Z,LIVRE 
XOR A 
RET 
LIVRE: 
LD A,1 
LD (DBOUNC),A 
LD A,255 
RET 
NAOPRE: 
XOR A 
LD (DBOUNC),A 
RET 
TIROS: 
LD HL,6916 
JR TIROS3 
TIROS2: 
LD HL,6920 
TIROS3: 
LD A,(YTR) 
DEC A 
CP 8 
JR C,FIMTR 
LD (YTR),A 
CALL WRTVRM 
LD A,(XTR) 
INC HL 
CALL WRTVRM 
RET 
FIMTR: 
LD A,209 
LD (YTR),A 
CALL WRTVRM 
RET 
;-------- BLOCO 8:IMPRIME NAVE 
IMPNAV: 
LD A,(XNAVE) 
LD HL,6913 
CALL WRTVRM 
LD HL,6925 
CALL WRTVRM 
LD HL,6929 
CALL WRTVRM 
LD A,(YNAVE) 
LD HL,6912 
CALL WRTVRM 
LD HL,6924 
CALL WRTVRM 
LD HL,6928 
ADD A,14 
CALL WRTVRM 
LD A,(PFOGO) 
INC A 
LD (PFOGO),A 
CP 20 
RET NZ 
XOR A 
LD (PFOGO),A 
LD HL,6930 
LD A,(CFOGO) 
INC A 
LD (CFOGO),A 
AND 1 
INC A 
ADD A,A 
ADD A,A 
CALL WRTVRM 
LD HL,6927 
CALL RDVRM 
INC A 
AND 15 
CALL WRTVRM 
RET 
;-------- BLOCO 9:INVASORES 
INVSR: 
XOR A 
LOOPIN: 
LD (LOPINV),A 
LD HL,ARRAY 
LD B,0 
ADD A,A 
ADD A,A 
LD C,A 
ADD HL,BC 
LD (PYINV),HL 
LD A,(HL) 
CP 209 
JR Z,PROXIN 
INC HL 
LD (PXINV),HL 
LD HL,IAPV 
LD A,(LOPINV) 
ADD A,A 
LD B,0 
LD C,A 
ADD HL,BC 
LD (APV),HL 
LD E,(HL) 
INC HL 
LD D,(HL) 
EX DE,HL 
LD A,(HL) 
CP 255 
JR Z,FIMINV 
PUSH AF 
LD A,(FLINC) 
AND A 
JR Z,NTINC 
INC HL 
NTINC: 
EX DE,HL 
POP AF 
LD (DIRI),A 
LD HL,(APV) 
LD (HL),E 
INC HL 
LD (HL),D 
LD HL,IARX 
LD A,(DIRI) 
LD C,A 
LD B,0 
ADD HL,BC 
LD B,(HL) 
LD HL,(PXINV) 
LD A,(HL) 
ADD A,B 
LD (HL),A 
LD HL,IARY 
LD A,(DIRI) 
LD C,A 
LD B,0 
ADD HL,BC 
LD B,(HL) 
LD HL,(PYINV) 
LD A,(HL) 
ADD A,B 
LD (HL),A 
LD HL,(PXINV) 
INC HL 
LD A,(DIRI) 
LD C,A 
AND A 
JR Z,$+7 
ADD A,8 
ADD A,A 
ADD A,A 
LD (HL),A 
LD A,C 
AND A 
CALL NZ,STBOMB 
PROXIN: 
LD A,(LOPINV) 
INC A 
CP LIINV 
JP NZ,LOOPIN 
XOR A 
LD (FLINC),A 
LD A,(PSINC) 
INC A 
LD (PSINC),A 
CP COMPV 
JR NZ,DSPINV 
LD A,1 
LD (FLINC),A 
XOR A 
LD (PSINC),A 
DSPINV: 
LD HL,ARRAY 
LD DE,6932 
LD BC,32 
CALL LDIRVM 
RET 
FIMINV: 
LD HL,(IVFASE) 
LD DE,32 
ADD HL,DE 
EX DE,HL 
LD HL,(APV) 
LD (HL),E 
INC HL 
LD (HL),D 
LD A,(YIFASE) 
LD HL,(PYINV) 
LD (HL),A 
LD A,(XIFASE) 
INC HL 
LD (HL),A 
RET 
;-------- BLOCO 10:MOVE ESTRELAS 
ESTRTM: 
LD HL,PAUSA 
TEMPO: 
DEC HL 
LD A,H 
OR L 
JR NZ,TEMPO 
ESTRE: 
LD A,(EVENTO) 
LOPEV: 
DEC A 
LD (EVENTO),A 
AND A 
RET NZ 
LD A,10 
LD (EVENTO),A 
LD HL,64 
CALL ROT 
LD A,(TOGGLE) 
CPL 
LD (TOGGLE),A 
AND A 
RET Z 
LD HL,128 
CALL ROT 
LD HL,6918 
CALL APGFM 
LD HL,6922 
APGFM: 
CALL RDVRM 
CP 68 
RET NZ 
DEC HL 
DEC HL 
LD A,209 
CALL WRTVRM 
RET 
ROT: 
LD B,63 
DEC HL 
CALL RDVRM 
LD C,A 
LOPROT: 
DEC HL 
CALL RDVRM 
INC HL 
CALL WRTVRM 
DEC HL 
DJNZ LOPROT 
LD A,C 
CALL WRTVRM 
RET 
;-------- BLOCO 11:SABOTADORES 
SABOT: 
LD A,(CSAB) 
INC A 
LD (CSAB),A 
CP 150 
RET NZ 
XOR A 
LD (CSAB),A 
LD A,(LISAB) 
AND A 
RET Z 
XOR A 
LOPSAB: 
LD (NSAB),A 
LD B,0 
LD C,A 
LD HL,ARSAB+1 
ADD HL,BC 
ADD HL,BC 
ADD HL,BC 
ADD HL,BC 
LD A,(HL) 
CP 255 
JR Z,PSAB 
CALL RANDOM 
AND 15 
JR Z,ATAQ 
LD A,(HL) 
LD B,A 
LD A,(XNAVE) 
CP B 
LD C,B 
JR C,SESQ 
LD A,(NSAB) 
ADD A,2 
LD B,A 
LD A,C 
INC A 
DJNZ $-1 
JR SSEGUE 
SESQ: 
LD A,(NSAB) 
ADD A,2 
LD B,A 
LD A,C 
DEC A 
DJNZ $-1 
SSEGUE: 
LD (HL),A 
INC HL 
LD A,(HL) 
CP 32 
JR NZ,$+4 
LD (HL),28 
LD B,(HL) 
LD A,52 
SUB B 
LD (HL),A 
PSAB: 
LD A,(NSAB) 
INC A 
LD C,A 
LD A,(LISAB) 
LD B,A 
LD A,C 
CP B 
JR C,LOPSAB 
LD HL,ARSAB 
LD DE,6996 
SLA B 
SLA B 
LD C,B 
LD B,0 
CALL LDIRVM 
RET 
ATAQ: 
INC HL 
LD (HL),32 
IN A,(170) 
LD B,50 
BIP: 
RES 7,A 
OUT (170),A 
SET 7,A 
OUT (170),A 
DJNZ BIP 
DEC HL 
LD B,(HL) 
LD A,(XNAVE) 
ADD A,8 
SUB B 
CP 16 
JR NC,PSAB 
LD A,1 
LD (STATX),A 
JR PSAB 
RET 
;-------- BLOCO 12:EXPLOSAO 
EXPLS: 
LD HL,6914 
LD A,68 
CALL WRTVRM 
LD A,10 
INC HL 
CALL WRTVRM 
LD HL,SDEXPL 
CALL STSND 
LD HL,60000 
TEXPL: 
DEC HL 
DEC HL 
INC HL 
LD A,H 
OR L 
JR NZ,TEXPL 
LD A,(NAVES) 
DEC A  ; NOP para vidas infinitas 
LD (NAVES),A 
AND A 
RET Z 
JP INNAVE 
;-------- BLOCO 13:SETA BOMBAS 
STBOMB: 
CALL RANDOM 
AND 31 
AND A 
RET NZ 
CALL RANDOM 
AND 7 
LD C,A 
LD A,(LIBOMB) 
LD B,A 
LD A,C 
CP B 
RET NC 
LD (NBOMB),A 
ADD A,A 
ADD A,A 
LD HL,IABOMB 
LD B,0 
LD C,A 
ADD HL,BC 
LD A,(HL) 
CP 209 
RET NZ 
PUSH HL 
LD HL,(PYINV) 
LD A,(HL) 
POP HL 
LD (HL),A 
INC HL 
PUSH HL 
LD HL,(PXINV) 
LD A,(HL) 
POP HL 
LD (HL),A 
LD HL,IARX 
LD A,(DIRI) 
LD C,A 
ADD HL,BC 
LD A,(HL) 
PUSH AF 
LD HL,IADB 
LD A,(NBOMB) 
LD C,A 
ADD HL,BC 
POP AF 
LD (HL),A 
RET 
;-------- BLOCO 14:MOVE BOMBAS 
ATBOMB: 
XOR A 
LPBOMB: 
LD (NBOMB),A 
ADD A,A 
ADD A,A 
LD B,0 
LD C,A 
LD HL,IABOMB 
ADD HL,BC 
LD A,(HL) 
CP 209 
JR Z,PRBOMB 
INC A 
CP 170 
JR Z,FIMBMB 
LD (HL),A 
INC HL 
AND 2 
AND A 
JR NZ,PRBOMB 
PUSH HL 
LD HL,IADB 
LD A,(NBOMB) 
LD C,A 
LD B,0 
ADD HL,BC 
LD B,(HL) 
POP HL 
LD A,(HL) 
ADD A,B 
LD (HL),A 
PRBOMB: 
LD A,(NBOMB) 
INC A 
LD C,A 
LD A,(LIBOMB) 
LD B,A 
LD A,C 
CP B 
JR NZ,LPBOMB 
JR IMPBMB 
FIMBMB: 
LD (HL),209 
JR PRBOMB 
IMPBMB: 
LD HL,IABOMB 
LD DE,6964 
LD BC,32 
CALL LDIRVM 
RET 
;-------- BLOCO 15:INICIA BOMBAS 
INIBMB: 
XOR A 
LPINIB: 
LD (NBOMB),A 
LD HL,IABOMB 
ADD A,A 
ADD A,A 
LD B,0 
LD C,A 
ADD HL,BC 
LD (HL),209 
INC HL 
INC HL 
LD (HL),20 
INC HL 
LD (HL),15 
LD A,(NBOMB) 
INC A 
CP 8 
JR NZ,LPINIB 
RET 
;-------- BLOCO 16:TESTA COLISAO 
TESTCH: 
CALL RDVDP 
BIT 5,A 
RET Z 
LD A,(YTIRO) 
CP 209 
JR Z,CPTR2 
LD (YTR),A 
LD A,(XTIRO) 
LD (XTR),A 
CALL CPTIRO 
LD (YTIRO),A 
LD A,(FLAC) 
AND A 
JR Z,CPTR2 
LD HL,6918 
LD A,68 
CALL WRTVRM 
CPTR2: 
LD A,(YTIRO2) 
CP 209 
JR Z,CPCOM 
LD (YTR),A 
LD A,(XTIRO2) 
LD (XTR),A 
CALL CPTIRO 
LD (YTIRO2),A 
LD A,(FLAC) 
AND A 
JR Z,CPCOM 
LD HL,6922 
LD A,68 
CALL WRTVRM 
CPCOM: 
LD HL,IABOMB 
LD (PTABLE),HL 
CALL CPBMB 
LD HL,ARRAY 
LD (PTABLE),HL 
CALL CPBMB 
RET 
CPBMB: 
XOR A 
LCBOMB: 
LD (NBOMB),A 
ADD A,A 
ADD A,A 
LD B,0 
LD C,A 
LD HL,(PTABLE) 
ADD HL,BC 
LD A,(HL) 
CP 209 
JR Z,CPRBMB 
LD B,A 
LD A,(YNAVE) 
SUB B 
ADD A,8 
CP 10 
JR NC,CPRBMB 
INC HL 
LD B,(HL) 
LD A,(XNAVE) 
SUB B 
ADD A,8 
CP 16 
JR NC,CPRBMB 
DEC HL 
LD (HL),209 
LD A,1 
LD (STATX),A 
CPRBMB: 
LD A,(NBOMB) 
INC A 
CP 8 
JR NZ,LCBOMB 
RET 
CPTIRO: 
XOR A 
LD (FLAC),A 
LPCPIN: 
LD (LOPINV),A 
ADD A,A 
ADD A,A 
LD B,0 
LD C,A 
LD HL,ARRAY 
ADD HL,BC 
LD A,(HL) 
CP 209 
JR Z,CPPIN 
LD B,A 
LD A,(YTR) 
SUB B 
ADD A,8 
CP 16 
JR NC,CPPIN 
INC HL 
LD B,(HL) 
LD A,(XTR) 
SUB B 
ADD A,8 
CP 16 
JR NC,CPPIN 
DEC HL 
LD (HL),209 
LD A,1 
LD (FLAC),A 
CALL ACERT 
CPPIN: 
LD A,(LOPINV) 
INC A 
CP 8 
JR NZ,LPCPIN 
LD A,(FLAC) 
AND A 
LD A,(YTR) 
RET Z 
LD A,209 
RET 
ACERT: 
LD A,(CONTDS) 
INC A 
LD (CONTDS),A 
LD HL,(PONTOS) 
LD DE,(PTFASE) 
ADD HL,DE 
LD (PONTOS),HL 
LD DE,(RECORD) 
AND A 
SBC HL,DE 
JR C,NAOREC 
ADD HL,DE 
LD (RECORD),HL 
NAOREC: 
CALL PLACAR 
RET 
;-------- BLOCO 17:INICIA INVAS. 
INIINV: 
XOR A 
LOPINI: 
LD (LOPINV),A 
ADD A,A 
LD HL,ARRAY 
LD B,0 
LD C,A 
ADD HL,BC 
ADD HL,BC 
LD A,(YIFASE) 
LD (HL),A 
INC HL 
LD A,(XIFASE) 
LD (HL),A 
INC HL 
INC HL 
LD A,(CFASE) 
LD (HL),A 
LD HL,IAPV 
ADD HL,BC 
LD (APV),HL 
LD HL,(IVFASE) 
ADD HL,BC 
EX DE,HL 
LD HL,(APV) 
LD (HL),E 
INC HL 
LD (HL),D 
LD A,(LOPINV) 
INC A 
CP LIINV 
JR NZ,LOPINI 
RET 
;-------- BLOCO 18:SONS 
STSND: 
LD A,(HL) 
CP 255 
RET Z 
INC HL 
LD E,(HL) 
PUSH HL 
CALL WRTPSG 
POP HL 
INC HL 
JR STSND 
;-------- BLOCO 19:PLACARES 
PLACAR: 
LD A,8 
OUT (153),A 
LD A,24 
OUT (153),A 
LD HL,(PONTOS) 
CALL IMPPL 
LD A,18 
OUT (153),A 
LD A,24 
OUT (153),A 
LD HL,(RECORD) 
CALL IMPPL 
RET 
IMPPL: 
LD DE,10000 
CALL DIGITO 
LD DE,1000 
CALL DIGITO 
LD DE,100 
CALL DIGITO 
LD DE,10 
CALL DIGITO 
LD DE,1 
DIGITO: 
LD A,48 
AND A 
CONTDI: 
SBC HL,DE 
JR C,FIMDIG 
INC A 
JR CONTDI 
FIMDIG: 
ADD HL,DE 
OUT (152),A 
RET 
;-------- BLOCO 20:INICIA FASE 
STFASE: 
LD A,(FASE) 
PUSH AF 
INC A 
CP 5 
JR NC,$+5 
LD (LIBOMB),A 
DEC A 
SRL A 
SRL A 
SRL A 
AND 3 
JR Z,$+5 
LD (LISAB),A 
POP AF 
AND 7 
LD B,0 
LD C,A 
LD HL,TPFASE 
ADD HL,BC 
LD A,(HL) 
LD (PTFASE),A 
LD HL,IPFASE 
ADD HL,BC 
ADD HL,BC 
LD A,(HL) 
LD (XIFASE),A 
INC HL 
LD A,(HL) 
LD (YIFASE),A 
LD HL,ACFASE 
ADD HL,BC 
LD A,(HL) 
LD (CFASE),A 
LD HL,DISPLT 
ADD HL,BC 
ADD HL,BC 
LD E,(HL) 
INC HL 
LD D,(HL) 
LD HL,IPVR 
ADD HL,DE 
LD (IVFASE),HL 
LD A,(FASE) 
INC A 
LD (FASE),A 
RET 


;-------- BLOCO 27:Monta tela
TELA:
INCLUDE "tela.inc"



;-------- BLOCO 22:ARRAYS Constantes

MSSPC: DEFM 'TECLE ESPACO'
MSTEL: DEFM ' PONTOS:00000 ' 
DEFM 'MAX:00000 NAVES: ' 
;
IARX: DEFB 0,0,1,1,1,0,-1,-1,-1 
IARY: DEFB 0,-1,-1,0,1,1,1,0,-1 
ARPC: DEFB 160,128,0,14,209,128,16,15,209,128,16,15,160,128,12,0,176,128,16,8 
ACFASE:DEFB 8,4,2,10,6,5,3,15 
TPFASE:DEFB 50,60,70,80,90,100,110,120 
IPFASE:DEFB 255,70,90,240,255,20,90,207,90,240,255,20,120,207,110,240 
DISPLT:DEFW 0,98H,14CH,200H,2BEH,370H,453H,50EH 
SDTIRO:DEFB 7,254,0,0,1,0,8,15,255 
SDEXPL:DEFB 7,247,6,31,8,16,12,32,13,0,255 
ARSAB: DEFB 175,20,24,2,175,80,28,13,175,220,24,08 

;-------- BLOCO 25:SPRITES
SPRITES:
INCLUDE "sprites.inc"

;-------- BLOCO 26:Trajetoria
TRAJ:
INCLUDE "traj.inc"


;-------- BLOCO 23:ROM LOADER


ORG 0x4000+8191
NOP


;-------- BLOCO 21:VARIAVEIS - RELOCADO
VARS:  EQU 0xE000


XNAVE:  EQU 0xE000 ; DEFB 0 
YNAVE:  EQU 0xE001 ; DEFB 0 
XTIRO:  EQU 0xE002 ; DEFB 0 
YTIRO:  EQU 0xE003 ; DEFB 0 
XTIRO2: EQU 0xE004 ; DEFB 0 
YTIRO2: EQU 0xE005 ; DEFB 0 
XTR:    EQU 0xE006 ; DEFB 0 
YTR:    EQU 0xE007 ; DEFB 0 
STATX:  EQU 0xE008 ; DEFB 0 
DBOUNC: EQU 0xE009 ; DEFB 0 
MODE:   EQU 0xE00A ; DEFB 0 
CONTDS: EQU 0xE00B ; DEFB 0 
EVENTO: EQU 0xE00C ; DEFB 0 
TOGGLE: EQU 0xE00D ; DEFB 0 
CCAN:   EQU 0xE00E ; DEFB 0 
LOPINV: EQU 0xE00F ; DEFB 0 
DIRI:   EQU 0xE010 ; DEFB 0 
FASE:   EQU 0xE011 ; DEFB 0 
CFASE:  EQU 0xE012 ; DEFB 0 
YIFASE: EQU 0xE013 ; DEFB 0 
XIFASE: EQU 0xE014 ; DEFB 0 
PSINC:  EQU 0xE015 ; DEFB 0 
CCINV:  EQU 0xE016 ; DEFB 0 
FLINC:  EQU 0xE017 ; DEFB 0 
PFOGO:  EQU 0xE018 ; DEFB 0 
CFOGO:  EQU 0xE019 ; DEFB 0 
NBOMB:  EQU 0xE01A ; DEFB 0 
;
NAVES:  EQU 0xE01B ; DEFB 0 
FLAC:   EQU 0xE01C ; DEFB 0 
CSAB:   EQU 0xE01D ; DEFB 0 
NSAB:   EQU 0xE01E ; DEFB 0 
; 
SEMT:   EQU 0xE020 ; DEFW 0 
CVRAM:  EQU 0xE022 ; DEFW 0 
IVFASE: EQU 0xE024 ; DEFW 0 
PXINV:  EQU 0xE026 ; DEFW 0 
PYINV:  EQU 0xE028 ; DEFW 0 
APV:    EQU 0xE02A ; DEFW 0 
PTABLE: EQU 0xE02C ; DEFW 0 
FTIRO:  EQU 0xE02E ; DEFW 0 
PONTOS: EQU 0xE030 ; DEFW 0 
RECORD: EQU 0xE032 ; DEFW 0 
PTFASE: EQU 0xE034 ; DEFW 0 

ARRAY:  EQU 0xE040 ; DEFS 32 
IAPV:   EQU 0xE060 ; DEFS 16 
IABOMB: EQU 0xE070 ; DEFS 32 
IADB:   EQU 0xE090 ; DEFS 8 

;ULTIMO EQU 0XE097 ; 

;-------- BLOCO 24:VARIAVEIS INICALIZADAS
; 
LIBOMB: EQU 0xE098 ;DEFB 1 
LISAB:  EQU 0xE099 ;DEFB 1 
INISEM: EQU 0xE09A ;DEFW 16384 
FIMSEM: EQU 0xE09C ;DEFW 32767 

FIMVARS: EQU 0XE09D 
;ULTIMO EQU 0XE09D ; 

END
